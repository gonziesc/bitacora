---
title: D√©cima clase
date: '2019-06-03'
description: 'Functores, is, pattern matching y polimorfismo. Findall, listas e is'
tags: [logico, functores, pattern-matching, polimorfismo, findall, is, listas]
---

## Tarea para la clase que viene
- Terminar el TP grupal.
- Hacer hasta la lecci√≥n 11 (inclusive) de [Mumuki](https://mumuki.io/pdep-utn/chapters/307-programacion-logica)

## Functores
Hasta ahora, en l√≥gico siempre hicimos consultas en las cuales nuestros individuos eran simples.
Pero adem√°s de los individuos simples, tambi√©n existen los **individuos complejos**. üßê
Los *functores* son individuos complejos y tienen:
- Un nombre que los identifica
- Una aridad

En nuestra base de conocimientos ten√≠amos libros, ahora conocemos los tipos de libros, que pueden ser:
- `novela(Genero, CantidadDePaginas)`
- `libroDeCuentos(CantidadDeCuentos)`
- `cientifico(Disciplina)` 
- `bestSeller(Precio, CantidadDePaginas)`

Adem√°s contamos con un predicado `tipo/2` que relaciona a cada libro con su tipo.

Ahora queremos saber si un libro est√° bueno, esto se cumple cuando:
- Es una novela policial y tiene m√°s de 150 p√°ginas.
- Es una novela de terror.
- Los libros con m√°s de 10 cuentos siempre son buenos.
- Es un libro cient√≠fico de fisicaCuantica.
- Es un best seller y el precio por p√°gina es menor a $50.

```prolog
estaBueno(Libro):-
    tipo(Libro, novela(policial, Paginas)),
    Paginas > 150.
estaBueno(Libro):-
    tipo(Libro, novela(terror, _)).
estaBueno(Libro):-
    tipo(Libro, libroDeCuentos(CantidadDeCuentos)),
    CantidadDeCuentos > 10.
estaBueno(Libro):-
    tipo(Libro, cientifico(fisicaCuantica)).
estaBueno(Libro):-
    tipo(Libro, bestSeller(Precio, Paginas)),
    Precio / Paginas < 50.
```
> Si bien los functores se escriben como un predicado, **NO** son un predicado. Como ven, los estamos usando como par√°metro en nuestras consultas.

Ahora, mirando üî≠ un poco a nuestro c√≥digo anterior, podemos ver l√≥gica repetida y eso no nos gusta mucho. üëé

Veamos c√≥mo podr√≠amos cambiar esto...


```prolog
estaBueno(Libro):-
    tipo(Libro, Tipo),
    esTipoCopado(Tipo).
esTipoCopado(novela(policial, Paginas)):-
    Paginas > 150.
esTipoCopado(novela(terror, _)).
esTipoCopado(libroDeCuentos(CantidadDeCuentos)):-
    CantidadDeCuentos > 10.
esTipoCopado(cientifico(fisicaCuantica)).
esTipoCopado(bestSeller(Precio, Paginas)):-
    Precio / Paginas < 50.
```

Tenemos un par de cosas interesantes aqu√≠. ü§î

Primero, si prestamos atenci√≥n a c√≥mo definimos el predicado `esTipoCopado`, podemos ver que estamos **deconstruyendo** (o abriendo) a los functores que representan el tipo de los libros.  
Por ejemplo, en esta cla√∫sula, el tipo va a unificar s√≥lo con aquellas novelas que sean policiales.
```prolog
esTipoCopado(novela(policial, Paginas)):-
    Paginas > 150.
% adem√°s la variable Paginas va a unificar
% con la cantidad de p√°ginas de esa novela
```
Adem√°s, cada functor va a "saber" con qu√© cla√∫sula del predicado quedarse.  
Todo esto gracias a que tenemos *Pattern Matching*. üéâ

En segundo lugar, podemos ver como al predicado `tipoCopado` no le importa para nada que *forma* puede llegar a tener el functor del tipo de libro.  
Al hecho de tratar variables indistintamente de su forma lo llamamos **polimorfismo** üíó y va a ser un concepto muy importante y √∫til desde ahora hasta siempre. üò±

## Findall
Ahora queremos saber el puntaje de un autor, este se calcula como `3 * cantidad de libros buenos que escribi√≥`.

Recordemos, que en nuestra base de conocimientos, contamos con un predicado `escribio/2` que relaciona a un autor con el libro que escribi√≥.

Podr√≠amos empezar escribiendo un predicado que relacione un autor con un libro bueno que haya escrito:
```prolog
escribioLibroBueno(Autor, Libro):-
    escribio(Autor, Libro),
    estaBueno(Libro).
```

Y en nuestra consola podemos hacer consultas como esta:
```prolog
?- escribioLibroBueno(Autor, Libro).
Autor = stephenKing,
Libro = it ;
Autor = horacioQuiroga,
Libro = cuentosDeLaSelva ;
Autor = stephenKing,
Libro = misery ;
Autor = stephenKing,
Libro = elJuegoDeGerald ;
Autor = stephenKing,
Libro = carrie .
```
Si bien, como `escribioLibroBueno` es inversible, podemos consultar por los valores que puede tomar la variable `Libro`. ¬øPero c√≥mo podr√≠amos trabajar con todos los libros buenos que escribi√≥ un autor al mismo tiempo? Bueno, podr√≠amos agruparlo en una **lista**!!

Para lograr esto contamos con un predicado llamado `findall/3`. Este se escribe de la forma `findall(Formato, Consulta, Lista)` y es inversible para su √∫ltimo argumento.

Entonces, ahora podr√≠amos escribir un predicado `librosBuenosQueEscribio/2` que relacione a un autor con todos los libros que escribi√≥ y que adem√°s est√°n buenos.

```prolog
librosBuenosQueEscribio(Autor, Libros):-
    autor(Autor),
    findall(Libro, escribioLibroBueno(Autor, Libro), Libros).
```
Y podemos consultar:

```prolog
?- librosBuenosQueEscribio(stephenKing, Libros).
Libros = [it, misery, carrie, elJuegoDeGerald]
% notar que tambi√©n podr√≠amos hacer consultas
% dejando al Autor como variable (es totalmente inversible)
```
Ahora, la cantidad de libros buenos que escribi√≥ ese autor ser√≠a el tama√±o de esa lista.  
Y bueno, ¬øc√≥mo creen que prolog podr√≠a relacionar a una lista con su tama√±o?  
S√≠, ¬°con un predicado ü§©! Contamos con `length(Lista, Tamanio)`, que es inversible para el tama√±o.

Entonces podr√≠amos escribir:
```prolog
cantidadDeLibrosBuenos(Autor, Cantidad):-
    librosBuenosQueEscribio(Autor, UnosLibros),
    length(UnosLibros, Cantidad).
```

Y ahora, por fin, ya podemos resolver nuestro problema inicial üòù:

```prolog
puntaje(Autor, Puntaje):-,
    cantidadDeLibrosBuenos(Autor, Cantidad),
    Puntaje is 3 * Cantidad.
```

## is
Pero, un momento, ¬øqu√© es esto de `is`?  
Bueno, `is` es un predicado que relaciona a una cuenta *(a la derecha)* con su resultado *(a la izquierda)*. Es inversible para el resultado: liga la variable del `Resultado` al resultado de la expresi√≥n matem√°tica de la derecha.
```prolog
Resultado is ExpresionMatematica
```
Entonces:
- ¬øCu√°ndo usamos `is`?  
Bueno, **solamente** cuando necesitamos realizar cuentas.
- ¬øY en en d√≥nde m√°s?  
En ning√∫n otro caso. Usar `is` s√≥lo para ligar variables est√° **MAL** y nadie quisiera reprobar su parcial de l√≥gico por eso. üëÆüèª‚Äç

#### Ejercicios
- Ahora queremos conocer el `promedioDePuntajes` que es el promedio de puntajes de todos los escritores.

```prolog
promedioDePuntajes(Promedio):-
  findall(Puntaje, puntaje(_, Puntaje), ListaPuntajes),
  sumlist(ListaPuntajes, Total),
  length(ListaPuntajes, CantidadPuntajes),
  Promedio is Total / CantidadPuntajes.
```

- Queremos saber si un escritor merece un nobel, esto sucede s√≥lo si todo lo que escribi√≥ es bueno. 
```prolog
mereceUnNobel(Autor) :-
  esAutor(Autor),
  forall(escribio(Autor, Libro), estaBueno(Libro)).
```

## Links √∫tiles
- [Wiki functores](http://wiki.uqbar.org/wiki/articles/paradigma-logico---functores.html)
- [Wiki polimorfismo](http://wiki.uqbar.org/wiki/articles/polimorfismo-en-el-paradigma-logico.html)
- [Wiki aritm√©tica/is](http://wiki.uqbar.org/wiki/articles/aritmetica-en-prolog.html)
- [Wiki findall/listas](https://github.com/uqbar-project/wiki/blob/master/wiki/articles/paradigma-logico---listas.md)
- [C√≥digo de la clase de hoy](https://gist.github.com/mnmallea/c2378a0afe713b483c3f233ca12d2f5e)
